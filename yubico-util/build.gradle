plugins {
  id 'java-library'
  id 'scala'
  id "io.freefair.lombok"
  id 'org.checkerframework' version '0.4.11' apply false
}

lombok {
  version = '1.18.8'
}

description = 'Yubico internal utilities'

project.ext.publishMe = true

dependencies {

  implementation(
    'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.9',
    'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.9',
    'com.upokecenter:cbor:2.4.1',
    'org.bouncycastle:bcprov-jdk15on:1.62',
    'org.slf4j:slf4j-api:1.7.25',
  )

  testImplementation(
    project(':yubico-util-scala'),
    'junit:junit:4.12',
    'org.scala-lang:scala-library:2.12.8',
    'org.scalacheck:scalacheck_2.12:1.14.0',
    'org.scalatest:scalatest_2.12:3.0.4',
  )
}

project.apply plugin: 'org.checkerframework'

dependencies {
  checkerFramework 'net.sridharan.objectconstruction:object-construction-checker:0.1.7-SNAPSHOT'
  checkerFramework 'org.checkerframework:checker:3.1.1-SNAPSHOT'
  implementation 'net.sridharan.objectconstruction:object-construction-qual:0.1.7-SNAPSHOT'
}

checkerFramework {
  checkers = ['org.checkerframework.checker.objectconstruction.ObjectConstructionChecker']
  extraJavacArgs = ['-AsuppressWarnings=type.anno.before.decl.anno', '-Awarns']
  skipVersionCheck = true
  excludeTests = true
}

if (project.hasProperty("cfShowChecks")) {
  checkerFramework {
    extraJavacArgs += ['-Ashowchecks']
  }
}

jar {
  manifest {
    attributes([
      'Implementation-Id': 'yubico-util',
      'Implementation-Title': project.description,
      'Implementation-Version': project.version,
      'Implementation-Vendor': 'Yubico',
    ])
  }
}

